; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

Set $UninstallProgram$ = $InstallDir$ + "\unins000.exe"

Message "Uninstalling " + $ProductId$ + " ..."

if FileExists($UninstallProgram$)
	; http://wpkg.org/Putty
	comment "The uninstall program is interactive if there is a real putty exe - so we substitute it"
	if FileExists($InstallDir$+"\putty.exe")
		killtask "putty.exe"
		Files_substitute_putty_exe
	endif
	comment "Uninstall program found, starting uninstall"
	Winbatch_uninstall
	sub_check_exitcode
	; normally we do not want to remove the profile entries (for example while update)
	;Registry_uninstall /sysnative /ALLNTUserDats
endif

comment "Delete files"
Files_uninstall /32Bit
comment "Delete kitty from start menu"
LinkFolder_uninstall

[Winbatch_uninstall]
;"$UninstallProgram$" /sp- /silent /norestart /SUPPRESSMSGBOXES /LOADINF="putty_uninstall"
"$UninstallProgram$" /sp- /silent /norestart /SUPPRESSMSGBOXES
;"$UninstallProgram$" /sp- /verysilent /norestart

[Files_uninstall]
del -sf "$InstallDir$\"
del -sf "%Programfiles32Dir%\kitty\"

[LinkFolder_uninstall]
set_basefolder common_programs
delete_subfolder "kitty"

[Files_substitute_putty_exe]
copy "%ScriptPath%\putty-substitute\putty.exe" "$InstallDir$"

[Registry_uninstall]
deletekey [HKEY_CURRENT_USER\Software\SimonTatham\PuTTY]
;deletekey [HKEY_CURRENT_USER\Software\Wow6432Node\SimonTatham\PuTTY]
; [HKEY_USERS\S-1-5-21-1080301877-4088584547-4076890939-500\Software\wow6432node\SimonTatham\PuTTY]
; [HKEY_USERS\PatchNTUserdatTempUser\Software\wow6432node\SimonTatham\PuTTY]
; [HKEY_USERS\.DEFAULT\Software\wow6432node\SimonTatham\PuTTY]

[Sub_check_exitcode]
comment "Test for installation success via exit code"
set $ExitCode$ = getLastExitCode
if ($ExitCode$ = "0")
	comment "ExitCode = "+$ExitCode$+" Setup was successfully run to completion."
else
	if ($ExitCode$ = "1")
		logError "ExitCode = "+$ExitCode$+" Setup failed to initialize."
		isFatalError " Setup failed to initialize."
	else
		if ($ExitCode$ = "2")
			logError "ExitCode = "+$ExitCode$+" The user clicked Cancel in the wizard before the actual installation started, or chose 'No' on the opening 'This will install...' message box."
			isFatalError "user canceled"
		else
			if ($ExitCode$ = "3")
				logError "ExitCode = "+$ExitCode$+" A fatal error occurred while preparing to move to the next installation phase (for example, from displaying the pre-installation wizard pages to the actual installation process). This should never happen except under the most unusual of circumstances, such as running out of memory or Windows resources."
				isFatalError "Setup failed"
			else
				if ($ExitCode$ = "4")
					logError "ExitCode = "+$ExitCode$+" A fatal error occurred during the actual installation process."
					isFatalError "Setup failed"
				else
					if ($ExitCode$ = "5")
						logError "ExitCode = "+$ExitCode$+" The user clicked Cancel during the actual installation process, or chose Abort at an Abort-Retry-Ignore box."
						isFatalError "user canceled"
					else
						if ($ExitCode$ = "6")
							logError "ExitCode = "+$ExitCode$+" The Setup process was forcefully terminated by the debugger (Run | Terminate was used in the IDE)."
							isFatalError "Setup failed"
						else
							logError "Fatal: Setup program gives an unknown exitcode unequal zero: " + $ExitCode$
							isFatalError "Unknown exitcode " + $ExitCode$
						endif
					endif
				endif
			endif
		endif
	endif
endif


